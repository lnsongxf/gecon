# ############################################################################
# (c) Chancellery of the Prime Minister 2012-2015                            #
#                                                                            #
# Authors: Grzegorz Klima, Karol Podemski, Kaja Retkiewicz-Wijtiwiak         #
# ############################################################################
# Stochastic growth model with Epstein-Zin preferences
# ############################################################################

options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};

tryreduce
{
    pi[], L_s[], K_d[], L_d[];
};

block CONSUMER
{
    definitions
    {
        u[] = (C[]^(1 - eta) - 1) / (1 - eta);
    };
    controls
    {
        K_s[], C[], I[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]^(1 - theta_EZ)]^(1 / (1 - theta_EZ));
    };
    constraints
    {
        I[] + C[] = pi[] + r[] * K_s[-1] + W[] * L_s[];
        K_s[] = (1 - delta) * K_s[-1] + I[];
    };
    identities
    {
        L_s[] = 1;
    };
    calibration
    {
        delta = 0.025;
        beta = 0.99;
        eta = 2;
        theta_EZ = 0.05;
    };
};

block FIRM
{
    controls
    {
        K_d[], L_d[], Y[];
    };
    objective
    {
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    constraints
    {
        Y[] = Z[] * K_d[]^alpha * L_d[]^(1 - alpha);
    };
    calibration
    {
        r[ss] * K_s[ss] = 0.36 * Y[ss] -> alpha;
    };
};

block EQUILIBRIUM
{
    identities
    {
        K_d[] = K_s[-1];
        L_d[] = L_s[];
    };
};

block EXOG 
{
    identities
    {
        Z[] = exp(phi * log(Z[-1]) + epsilon_Z[]);
    };
    shocks
    {
        epsilon_Z[];
    };
    calibration
    {
        phi = 0.95;
    };
};
