# ###################################################################
# (c) Kancelaria Prezesa Rady Ministrów 2012-2015                   #
# Treść licencji w pliku:                                           #
# http://gecon.r-forge.r-project.org/files/gEcon_licence.txt        #
#                                                                   #
# (c) Chancellery of the Prime Minister 2012-2015                   #
# Licence terms can be found in the file:                           #
# http://gecon.r-forge.r-project.org/files/gEcon_licence.txt        #
#                                                                   #
# Author(s): Marta Retkiewicz                                       #
# ###################################################################
# Simple CGE model (2x3x2) with Cobb-Douglas production function
# ###################################################################


options
{
    output LaTeX = TRUE;
    output LaTeX landscape = TRUE;
	output logfile = TRUE;
};

indexsets
{
    SEC = { 'A' .. 'C' };
    HH = { '1' .. '2' };
};

block <h::HH> CONSUMER
{
    definitions
    {
        u<h>[] = (SUM<s::SEC>(alpha<s, h> * D<s, h>[]^((omega - 1)/omega)))^(omega/(omega - 1));
    };
    controls
    {
        <s::SEC>D<s, h>[]; # Household h demand for good s
    };
    objective
    {
        U<h>[] = u<h>[];
    };
    constraints
    {
        INC<h>[] + PI<h>[] = SUM<s::SEC>(D<s, h>[] * p<s>[]);
    };
    identities
    {
        INC<h>[] = K<h>[] * p_k[] + L<h>[];
        K<h>[] = ks_data<h>;
        L<h>[] = ls_data<h>;
    };
    calibration
    {
        <s::SEC\'A'>D<s, h>[] = d_data<s, h> -> alpha<s, h>;
        SUM<s::SEC>(alpha<s, h>^omega) = 1 -> alpha<'A', h>;
    };
};

block <s::SEC>FIRM
{
    controls
    {
        Y<s>[], K<s>[], L<s>[], <si::SEC>X<si, s>[];
    };
    objective
    {
        pi<s>[] = Y<s>[] * p<s>[] - (K<s>[] * p_k[] + L<s>[]) - SUM<si::SEC>(X<si, s>[] * p<si>[]);
    };
    constraints
    {
        Y<s>[] = gamma<s> * K<s>[]^beta_k<s> * L<s>[]^beta_l<s> * PROD<si::SEC>(X<si, s>[]^beta_x<si, s>);
    };
    calibration
    {
        beta_k<s> + beta_l<s> + SUM<si::SEC>(beta_x<si, s>) = 1 -> beta_k<s>;
        L<s>[] = l_data<s> -> beta_l<s>;
        <si::SEC>X<si, s>[] = x_data<si, s> -> beta_x<si, s>;
        Y<s>[] = y_data<s> -> gamma<s>;
    };
};

block EQUILIBRIUM
{
    identities
    {
        SUM<h::HH>(K<h>[]) = SUM<s::SEC>(K<s>[]);
        <s::SEC>p<s>[] = 1;
        <h::HH>PI<h>[] = SUM<s::SEC>(pi<s>[]) * pi_h<h>;
    };
    calibration
    {
        SUM<h::HH>(pi_h<h>) = 1 -> pi_h<'1'>;
    }
};


