# ############################################################################
# (c) Chancellery of the Prime Minister 2012-2015                            #
#                                                                            #
# Authors: Marta Retkiewicz                                                  #
# ############################################################################
# Simple CGE model with Leontief production function
# ############################################################################


options
{
    output LaTeX = TRUE;
    output LaTeX landscape = TRUE;
    output logfile = TRUE;
};

indexsets
{
    SEC = { 'A' .. 'C' };
    HH = { '1' .. '2' };
};

block <h::HH> CONSUMER
{
    definitions
    {
        u<h>[] = (SUM<s::SEC>(alpha<s, h> * D<s, h>[]^((omega - 1)/omega)))^(omega/(omega - 1));
    };
    controls
    {
        <s::SEC>D<s, h>[]; # Household h demand for good s
    };
    objective
    {
        U<h>[] = u<h>[];
    };
    constraints
    {
        INC<h>[] + PI<h>[] = SUM<s::SEC>(D<s, h>[] * p<s>[]);
    };
    identities
    {
        INC<h>[] = K<h>[] * p_k[] + L<h>[];
        K<h>[] = ks_data<h>;
        L<h>[] = ls_data<h>;
    };
    calibration
    {
        <s::SEC\'A'>D<s, h>[] = d_data<s, h> -> alpha<s, h>;
        SUM<s::SEC>(alpha<s, h>^omega) = 1 -> alpha<'A', h>;
        SUM<hh::HH>(pi_h<hh>) = 1 -> pi_h<'1'>;
    };
};

block <s::SEC>FIRM
{
    controls
    {
        Y<s>[], K<s>[], L<s>[], Y_VA<s>[], Y_INT<s>[];
    };
    objective
    {
        pi<s>[] = Y<s>[] * p<s>[] - (K<s>[] * p_k[] + L<s>[]) - SUM<si::SEC>(Y_INT<s>[] * p<si>[] / beta_x<si, s>);
    };
    constraints
    {
        # A two-level Leontief production function
        # Y = min{Y_VA, Y_INT}
        Y<s>[] = Y_VA<s>[];
        Y<s>[] = Y_INT<s>[];
        Y_VA<s>[] = gamma_yva<s> * K<s>[]^beta_k<s> * L<s>[]^beta_l<s>;
    };
    identities
    {
        # Y_INT = min{<s::SEC>(X<si, s> * beta_x<si, s>)}
        <si::SEC>X<si, s>[] = Y_INT<s>[] / beta_x<si, s>;
    };
    calibration
    {
        beta_k<s> + beta_l<s> = 1 -> beta_k<s>;
        L<s>[] = l_data<s> -> beta_l<s>;
        Y_VA<s>[] = y_data<s> -> gamma_yva<s>;
        
        <si::SEC>X<si, s>[] = x_data<si, s> -> beta_x<si, s>;
    };
};

block EQUILIBRIUM
{
    identities
    {
        SUM<h::HH>(K<h>[]) = SUM<s::SEC>(K<s>[]);
        <s::SEC>p<s>[] = 1;
        <h::HH>PI<h>[] = SUM<s::SEC>(pi<s>[]) * pi_h<h>;
    };
};

