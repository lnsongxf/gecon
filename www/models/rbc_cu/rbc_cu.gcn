# ###################################################################
# (c) Kancelaria Prezesa Rady Ministrów 2012-2014                   #
# Treść licencji w pliku:                                           #
# http://gecon.r-forge.r-project.org/files/gEcon_licence.txt        #
#                                                                   #
# (c) Chancellery of the Prime Minister 2012-2014                   #
# Licence terms can be found in the file:                           #
# http://gecon.r-forge.r-project.org/files/gEcon_licence.txt        #
#                                                                   #
# Author(s): Grzegorz Klima, Karol Podemski,                        #
#            Kaja Retkiewicz-Wijtiwiak                              #
# ###################################################################
# Basic RBC model with capacity utilization
# ###################################################################

options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};

tryreduce
{
    P[], L_d[], pi[];
};

block CONSUMER
{
    definitions
    {
        u[] = (C[]^mu * (1 - L_s[])^(1 - mu)) ^ (1 - eta) / (1 - eta);
    };
    controls
    {
        C[], L_s[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]]     : lambda_U[];
    };
    constraints
    {
        C[] = W[] * L_s[] + pi[]         : lambda_c[];
    };
    calibration
    {
        beta = 0.99;
        eta = 2;
        mu = 0.3;
    };
};

block FIRM
{
    controls
    {
        K[], L_d[], Y[], I[], pi[], CapUt[];
    };
    objective
    {
        PI[] = pi[] + E[][lambda_U[1]  * lambda_c[1] / lambda_c[] * PI[1]];
    };
    constraints
    {
        Y[] = Z[] ^ (1 - alpha) * (K[-1] * CapUt[])^alpha * (L_d[])^(1 - alpha);
        K[] = (1 - delta * CapUt[] ^ omega) * K[-1]  + I[];
        pi[] = P[] * Y[] - L_d[] * W[] - I[];
    };
    calibration
    {
        alpha = 0.36;
        delta = 0.025;
        omega = 1.45;
    };
};

block EQUILIBRIUM
{
    identities
    {
        P[] = 1;
        L_d[] = L_s[];
    };
};

block EXOG 
{
    identities
    {
        Z[] = exp(phi * log(Z[-1]) + epsilon_Z[]);
    };
    shocks
    {
        epsilon_Z[];
    };
    calibration
    {
        phi = 0.95;
    };
};
